// __multiversion__
// This signals the loading code to prepend either #version 100 or #version 300 es as apropriate.

#include "fragmentVersionSimple.h"
#include "uniformPerFrameConstants.h"
#include "natural-mystic-config.h"
#include "natural-mystic-noise.h"

varying vec4 skyColor;
varying vec4 cloudColor;
varying vec3 worldPos;
varying float camDist;

/* Generate a pattern of clouds based on a world position. */
highp float clouds(highp float time, highp vec3 pos) {
    /* Use of highp is essential here, as the uniform TIME in mediump
     * starts to lose precision within 10 minutes.
     */
    const highp vec2 resolution = vec2(0.8, 0.8);
    const int octaves = 6;

    highp vec2 st = pos.xz / resolution;
    st.x *= resolution.x / resolution.y;
    /* The inverse of the speed (512) should be a power of two in
     * order to avoid a precision loss.
     */
    st.y += time / 512.0;

    /* We intentionally throw away some
     * of the precision so we get somewhat sparse noise.
     */
    highp float result = fBM(octaves, st * 3.0);
    result = smoothstep(0.5, 1.0, result) * 4.0;
    result = clamp(result, 0.0, 1.0);

    /* Workaround for MCPE-39749: the uniform TIME might not be a
     * number. See https://bugs.mojang.com/browse/MCPE-39749
     */
    result = isnan(result) || isinf(result) ? 0.0 : result;
    return result;
}

void main() {
#if defined(ENABLE_FBM_CLOUDS)

    /* Clouds near the horizon should be blended back to the sky
     * color, or otherwise the planar nature of the sky plane will be
     * even more apparent. */
    highp float density = clouds(TIME, worldPos);
    density = mix(density, 0.0, smoothstep(0.9, 1.0, camDist));

    gl_FragColor = mix(skyColor, cloudColor, density);

#else
    gl_FragColor = skyColor;
#endif /* ENABLE_FBM_CLOUDS */
}

// Local Variables:
// mode: glsl
// indent-tabs-mode: nil
// End:
